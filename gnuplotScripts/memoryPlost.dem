reset
unset key
bm = 0.15
lm = 0.12
rm = 0.95
gap = 0.03
size = 0.75
y1 = 0.0; y2 = 70; y3 = 120; y4 = 150
set output "memory.pdf"
set terminal pdf
set datafile missing '-'
set style data linespoints
set multiplot
set xlabel "Dataset"
set border 1+2+8
set xtics nomirror
set ytics 20
set lmargin at screen lm
set rmargin at screen rm
set bmargin at screen bm
set tmargin at screen bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) )




 set linetype  1 lc rgb "#a6cee3" lw 1
 set linetype  2 lc rgb "#1f78b4" lw 1
 set linetype  3 lc rgb "#b2df8a" lw 1
 set linetype  4 lc rgb "#33a02c" lw 1
 set linetype  5 lc rgb "#fb9a99" lw 1
 set linetype  6 lc rgb "#e31a1c" lw 1
 set linetype  7 lc rgb "#fdbf6f" lw 1
 set linetype  8 lc rgb "#ff7f00" lw 1
 set linetype  9 lc rgb "#cab2d6" lw 1
 set linetype  10 lc rgb "#6a3d9a" lw 1


set yrange [y1:y2]
plot "memory/spades/allMemory.dat" using 2:xtic(1) title columnheader(2)  , for [i=3:11] '' using i title columnheader(i) lw 1 ps .5 

unset xtics
unset xlabel
set border 2+4+8
set bmargin at screen bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap
set tmargin at screen bm + size + gap
set yrange [y3:y4]

set label 'Peak memory (GB)' at screen 0.03, bm + 0.5 * (size + gap) offset 0,-strlen("Peak memory (GB)")/4.0 rotate by 90

set arrow from screen lm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0 to screen \
lm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 nohead

set arrow from screen lm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0  + gap to screen \
lm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 + gap nohead

set arrow from screen rm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0 to screen \
rm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 nohead

set arrow from screen rm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0  + gap to screen \
rm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 + gap nohead
set key invert reverse at 5.5,145
set key font ", 7"
plot "memory/spades/allMemory.dat" using 2:xtic(1) title columnheader(2)  , for [i=3:11] '' using i title columnheader(i) lw 1 ps .5 
unset multiplot